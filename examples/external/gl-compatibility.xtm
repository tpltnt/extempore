;;; gl-compatibility.xtm -- old-style OpenGL example

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;; This example uses the (now very old) "fixed function" graphics
;; pipeline. Unless there's a reason you have to use this
;; functionality, then you probably want to look at the
;; "hello-triangle.xtm" example instead

;;; Code:

(sys:load "libs/external/glfw3.xtm")

(bind-val window GLFWwindow*
  (glfw_init_and_create_interaction_compatibility_window 1024 768))

(sys:load "libs/external/glcompat.xtm")

(bind-func gl_draw
  (let ((size 0.01))
    (lambda (degree)
      (glClear (+ GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
      (glLoadIdentity)
      (glTranslatef 0.0 -1.0 0.0)
      (let ((i:float 0.0))
        (dotimes (i 1000.0)
          (glTranslatef (/ i 2000.0) 0.0 0.0)
          (glColor3f (/ i 1500.0) 0.0 1.0)
          (glRotatef degree (/ i 200000.0) 0.5 0.0)
          (glBegin GL_QUADS)
          (glVertex2f 0.0 0.0)
          (glVertex2f size 0.0)
          (glVertex2f size size)
          (glVertex2f 0.0 size)
          (glEnd)))
      (glfwSwapBuffers window))))

;; standard Extempore callback

(bind-func gl_draw_loop
  (lambda (time:i64 delta_t:double degree)
    (gl_draw degree)
    (let ((next_time (+ time (convert (* 44100. delta_t)))))
      (callback next_time gl_draw_loop next_time delta_t (+ degree 0.005)))))

(gl_draw_loop (now) (/ 1. 30.) 70.)

;; to stop it...

;; (bind-func gl_draw_loop
;;   (lambda (time:i64 delta_t:double)
;;     (println "gl_draw_loop callback stopped")
;;     #f))

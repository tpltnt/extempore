;;; triangle.xtm -- basic OpenGL drawing and window management

;; Author: Ben Swift
;; Keywords: extempore
;; Required dylibs: libGL, libglfw3

;;; Commentary:

;; This is the simplest triangle example using the Core (3.2+) OpenGL
;; profile. There are some nice xtlang helper types/functions in
;; gl/gl-objects.xtm which make this a bit cleaner - see
;; libs/external/shader-tutorials/triangle.xtm for an example.

;; This file is useful as a a minimal working example for debugging
;; problems in your OpenGL setup

;;; Code:

(sys:load "libs/external/glfw3.xtm")

(bind-val window GLFWwindow*
  (glfw_init_and_create_interaction_window 800 600))

(sys:load "libs/external/gl.xtm")

;; shader stuff

(bind-val triangle_vert_data |6,float|)

(bind-val vbo i32)
(bind-val vao i32)
(bind-val sp i32)

;; init data
(call-as-xtlang
 (let ((vbo_ptr:i32* (salloc))
       (vao_ptr:i32* (salloc)))
   (afill! triangle_vert_data
         ;; pos
         0.0  0.5
         0.5 -0.5
         -0.5 -0.5)
   ;; VBO
   (glGenBuffers 1 vbo_ptr)
   (set! vbo (pref vbo_ptr 0))
   (glBindBuffer GL_ARRAY_BUFFER vbo)
   (glBufferData GL_ARRAY_BUFFER (* 6 4) (convert (aref-ptr triangle_vert_data 0) i8*) GL_STREAM_DRAW)
   ;; VAO
   (glGenVertexArrays 1 vao_ptr)
   (set! vao (pref vao_ptr 0))
   (glBindVertexArray vao)
   ;; (glBindBuffer GL_ARRAY_BUFFER vbo)
   (glVertexAttribPointer 0 2 GL_FLOAT GL_FALSE 0 null)
   (glEnableVertexAttribArray 0))
 void)

(bind-func create_triangle_shader
  (lambda (vsource:i8* fsource:i8*)
    (let ((vert (glCreateShader GL_VERTEX_SHADER))
          (frag (glCreateShader GL_FRAGMENT_SHADER))
          (source_ptr:i8** (salloc))
          (program (glCreateProgram)))
      ;; vert shader
      (pset! source_ptr 0 vsource)
      (glShaderSource vert 1 source_ptr (cast null i32*))
      (glCompileShader vert)
      ;; frag shader
      (pset! source_ptr 0 fsource)
      (glShaderSource frag 1 source_ptr (cast null i32*))
      (glCompileShader frag)
      (glAttachShader program vert)
      (glAttachShader program frag)
      (glLinkProgram program)
      (set! sp program))))

(create_triangle_shader
 "#version 400

layout(location = 0) in vec2 vertex_position;

void main () {
  gl_Position = vec4(vertex_position, 0.0, 1.0);
}"
 "#version 400

out vec4 frag_colour;

void main () {
  frag_colour = vec4(1.,1.,1.,1.);
}")

(bind-func gl_draw_loop
  (lambda (time:i64 delta_t:double)
    (glClear GL_COLOR_BUFFER_BIT)
    (glUseProgram sp)
    (glBindVertexArray vao)
    (glDrawArrays GL_TRIANGLES 0 3)
    (glfwPollEvents)
    (glfwSwapBuffers window)
    (let ((next_time (+ time (convert (* 44100. delta_t)))))
      (callback next_time gl_draw_loop next_time delta_t))))

(gl_draw_loop (now) (/ 1.0 30.0)) ;; 30fps
